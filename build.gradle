plugins {
    id 'org.springframework.boot' version '2.4.1'
}

apply plugin: 'java'
apply plugin: 'io.spring.dependency-management'

group 'com.peyerroger'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

sourceSets {
    testIntegration {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

configurations {
    testIntegrationImplementation.extendsFrom implementation
    testIntegrationRuntimeOnly.extendsFrom runtimeOnly
}

dependencies {
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa'

    runtimeOnly group: 'com.h2database', name: 'h2'

    compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.16'
    compileOnly group: 'org.mapstruct', name: 'mapstruct', version: '1.4.1.Final'

    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.16'
    annotationProcessor group: 'org.mapstruct', name: 'mapstruct-processor', version: '1.4.1.Final'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: '3.7.0'
    testImplementation group: 'org.mapstruct', name: 'mapstruct', version: '1.4.1.Final'

    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'

    testIntegrationImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
}

task testIntegration(type: Test) {
    description = 'Runs integration tests.'
    group = 'verification'

    testClassesDirs = sourceSets.testIntegration.output.classesDirs
    classpath = sourceSets.testIntegration.runtimeClasspath
    shouldRunAfter test
}



testIntegration {
    useJUnitPlatform()
}

test {
    useJUnitPlatform()
}

bootBuildImage {
    imageName = "gcr.io/items-api/${project.name}:${project.version}"
}

check.dependsOn testIntegration
